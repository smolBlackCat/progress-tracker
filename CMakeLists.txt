cmake_minimum_required(VERSION 3.10)
project(Progress VERSION 2.0.0)

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED True)

# General Settings
option(DEVELOPMENT "Build the application in development mode" ON)
option(FLATPAK "Defines whether the application is a flatpak distribution" OFF)

if (DEVELOPMENT)
    set(APP_NAME "Progress (Debug)")
    set(APPLICATION_ID "io.github.smolblackcat.Progress.Devel")
    set(APP_ICON io.github.smolblackcat.Progress.Devel.svg)
    set(EXECUTABLE_NAME "progress-debug")
    set(CMAKE_BUILD_TYPE "Debug")
else()
    set(APP_NAME "Progress")
    set(APPLICATION_ID "io.github.smolblackcat.Progress")
    set(APP_ICON io.github.smolblackcat.Progress.svg)
    set(EXECUTABLE_NAME "progress")
    set(CMAKE_BUILD_TYPE "Release")
endif()

find_program(GLIB_COMPILE_RESOURCES NAMES glib-compile-resources)
find_program(GLIB_COMPILE_SCHEMAS NAMES glib-compile-schemas)
find_package(PkgConfig REQUIRED)
pkg_check_modules(GTKMM REQUIRED gtkmm-4.0)
pkg_check_modules(ADWAITA REQUIRED libadwaita-1)

find_package(Intl REQUIRED)
find_package(Catch2 3)

find_library(TINYXML2 libtinyxml2 tinyxml2 REQUIRED)

if (LINUX)
    include(GNUInstallDirs)
    set(LOCALE_FOLDER "${CMAKE_INSTALL_PREFIX}/${CMAKE_INSTALL_DATAROOTDIR}/locale/")
elseif (WIN32)
    set(LOCALE_FOLDER "./locales/")
endif()

add_subdirectory(src)
add_subdirectory(po)
add_subdirectory(data)

if(Catch2_FOUND AND DEVELOPMENT)
    add_subdirectory(test)
    include(CTest)
    add_test(NAME TestBoard COMMAND test/board-test)
    add_test(NAME TestCardlist COMMAND test/cardlist-test)
    add_test(NAME TestCard COMMAND test/card-test)
    add_test(NAME TestModification COMMAND test/modification-test)
endif()

if (LINUX)
    set(CPACK_GENERATOR "DEB" "TXZ")
    set(CPACK_DEBIAN_PACKAGE_MAINTAINER "Gabriel de Moura")
    set(CPACK_DEBIAN_PACKAGE_NAME "progress-tracker")
    set(CPACK_DEBIAN_FILE_NAME "progress-tracker-${Progress_VERSION_MAJOR}.${Progress_VERSION_MINOR}.deb")
    set(CPACK_DEBIAN_PACKAGE_VERSION "${Progress_VERSION_MAJOR}.${Progress_VERSION_MINOR}.${Progress_VERSION_PATCH}")
    set(CPACK_DEBIAN_PACKAGE_DESCRIPTION "Simple app for kanban todo lists")
    set(CPACK_DEBIAN_PACKAGE_DEPENDS "libgtkmm-4.0-0, libadwaita-1-0, libtinyxml2-10")
    set(CPACK_DEBIAN_PACKAGE_SECTION "gnome")
    set(CPACK_DEBIAN_PACKAGE_HOMEPAGE "https://github.com/smolBlackCat/progress-tracker")
elseif(WIN32)
    set(CPACK_GENERATOR "NSIS64" "ZIP")

    if (DEVELOPMENT)
        set(APP_WINDOWS_ICON io.github.smolblackcat.Progress.Devel.ico)
    else()
        set(APP_WINDOWS_ICON io.github.smolblackcat.Progress.ico)
    endif()

    include(InstallRequiredSystemLibraries)
    set(CPACK_PACKAGE_NAME "Progress")
    set(CPACK_PACKAGE_VENDOR "smolblackcat")
    # set(CPACK_PACKAGE_ICON ${CMAKE_SOURCE_DIR}/data/io.github.smolblackcat.Progress.io)
    set(CPACK_PACKAGE_DESCRIPTION_SUMMARY "Simple app for kanban todo lists")
    set(CPACK_PACKAGE_VERSION "${Progress_VERSION_MAJOR}.${Progress_VERSION_MINOR}.${Progress_VERSION_PATCH}")
    set(CPACK_PACKAGE_INSTALL_DIRECTORY "Progress")
    set(CPACK_RESOURCE_FILE_LICENSE "${CMAKE_SOURCE_DIR}/LICENSE")
    set(CPACK_RESOURCE_FILE_README "${CMAKE_SOURCE_DIR}/README.md")

    # NSIS specific options
    set(CPACK_NSIS_DISPLAY_NAME "Progress")
    set(CPACK_NSIS_PACKAGE_NAME "Progress")
    set(CPACK_NSIS_CONTACT "smolblackcat@proton.me")
    set(CPACK_NSIS_ENABLE_UNINSTALL_BEFORE_INSTALL ON)
    set(CPACK_NSIS_MODIFY_PATH OFF)
    set(CPACK_NSIS_EXTRA_INSTALL_COMMANDS "WriteRegStr HKLM 'Software\\\\Microsoft\\\\Windows\\\\CurrentVersion\\\\Uninstall\\\\Progress' 'DisplayIcon' '$INSTDIR\\\\${APP_WINDOWS_ICON}'")
    set(CPACK_NSIS_CREATE_ICONS_EXTRA "\
  CreateShortCut '$SMPROGRAMS\\\\Progress.lnk' '$INSTDIR\\\\progress-tracker.exe' '' '$INSTDIR\\\\${APP_WINDOWS_ICON}' 0 '' '' 'Progress' SW_SHOWNORMAL
")
    set(CPACK_NSIS_DELETE_ICONS_EXTRA "Delete '$SMPROGRAMS\\\\Progress.lnk'")
    set(CPACK_NSIS_MUI_ICON "${CMAKE_SOURCE_DIR}/data/io.github.smolblackcat.Progress.ico")
    set(CPACK_NSIS_HELP_LINK "https://github.com/smolBlackCat/progress-tracker/issues")
    set(CPACK_NSIS_URL_INFO_ABOUT "https://github.com/smolBlackCat/progress-tracker")
endif()
include(CPack)
